<!DOCTYPE html>
<html lang="en">
<head>
	<title>Shader 3dViewer : View GLSL shaders on 3D objects, with lights</title>
	<meta charset="utf-8">
	<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
	<meta name="author" content="liv@tailless.co.uk">
	<style>
	body {
		margin: 0px;
		overflow: hidden;
	}
	</style>
</head>
<body>

	<div id="container"></div>

	<script src="./js/libs/three.min.js"></script>
	<script src="./js/controls/OrbitControls.js"></script>

	<script src="./js/Detector.js"></script>
	<script src="./js/libs/stats.min.js"></script>
	<script src="./js/libs/dat.gui.min.js"></script>

	<script id="vertexShader" type="x-shader/x-vertex">

	varying vec2 vUv;
	varying vec3 vecPos;
	varying vec3 vecNormal;

	void main() {
		vUv = uv;

		// vecNormal = normalize( worldNormal );,

		vecPos = (modelViewMatrix * vec4(position, 1.0)).xyz;
		vecNormal = (modelViewMatrix * vec4(normal, 0.0)).xyz;
		gl_Position = projectionMatrix * vec4(vecPos, 1.0);
	}

	</script>


	<!--Basic  Gradiant with lambertian lighting...-->
	<script id="fragmentShaderGradiant" type="x-shader/x-fragment">

	precision highp float;

	varying vec2 vUv;
	varying vec3 vecPos;
	varying vec3 vecNormal;

	uniform vec2 u_resolution;
	uniform float u_time;
	uniform float u_alpha;

	uniform float u_lightIntensity;

	struct PointLight {
		vec3 color;
		vec3 position; // light position, in camera coordinates
		float distance; // used for attenuation purposes.
	};

	uniform PointLight pointLights[NUM_POINT_LIGHTS];


	void main(void) {
		// Pretty basic lambertian lighting...
		vec4 addedLights = vec4(0.0, 0.0, 0.0, 1.0);

		for(int l = 0; l < NUM_POINT_LIGHTS; l++) {
			vec3 lightDirection = normalize(vecPos  - pointLights[l].position);
			addedLights.rgb += clamp(dot(-lightDirection, vecNormal), 0.0, 1.0) * pointLights[l].color * u_lightIntensity;
		}

		//gl_FragColor = texture2D(textureSampler, vUv) * addedLights;
		vec2 position = vUv;
		vec3 col = vec3( abs(sin(u_time * 0.1)), position);
		gl_FragColor = vec4( col, u_alpha) * addedLights;
	}


	</script>


	<!--simple gradiant-->


	<!--Colorised Quasicrystal-->
	<script id="fragmentShader_Quasi" type="x-shader/x-fragment">

	precision highp float;

	varying vec2 vUv;
	varying vec3 vecPos;
	varying vec3 vecNormal;

	uniform vec2 u_resolution;
	uniform float u_time;
	uniform float u_alpha;

	uniform float u_lightIntensity;

	struct PointLight {
		vec3 color;
		vec3 position; // light position, in camera coordinates
		float distance; // used for attenuation purposes.
	};

	uniform PointLight pointLights[NUM_POINT_LIGHTS];


	float wave(vec2 p, float angle) {
		vec2 direction = vec2(cos(angle), sin(angle));
		return cos(dot(p, direction));
	}

	float wrap(float x) {
		return abs(mod(x, 2.)-1.);
	}

	void main() {

		vec2 position = vUv;
		vec2 p = (position - 0.5) * 22.;

		float brightness = 0.;
		for (float i = 1.; i <= 5.; i++) {
			brightness += wave(p, u_time * 0.1 / i);
		}

		brightness = wrap(brightness);

		//add light
		vec4 addedLights = vec4(0.0, 0.0, 0.0, 1.0);

		for(int l = 0; l < NUM_POINT_LIGHTS; l++) {
			vec3 lightDirection = normalize(vecPos  - pointLights[l].position);
			addedLights.rgb += clamp(dot(-lightDirection, vecNormal), 0.0, 1.0) * pointLights[l].color * u_lightIntensity;
		}

		//vec3 col = vec3( 0.5, position);
		//gl_FragColor = vec4( (vec3(col) * brightness), u_alpha ) * addedLights;
		gl_FragColor = vec4( vec3( brightness), u_alpha ) * addedLights;
	}
	</script>

	<script type="x-shader/x-vertex" id="fragmentDisplacement">

	precision highp float;

	varying vec2 vUv;
	varying vec3 vecPos;
	varying vec3 vecNormal;

	uniform vec2 u_resolution;
	uniform float u_time;
	uniform float u_alpha;

	uniform float u_lightIntensity;

	uniform sampler2D u_tImage;
	uniform sampler2D u_tDisplacement;


	struct PointLight {
		vec3 color;
		vec3 position; // light position, in camera coordinates
		float distance; // used for attenuation purposes.
	};

	uniform PointLight pointLights[NUM_POINT_LIGHTS];

	void main() {

		const float AMOUNT = 0.8;

		vec2 position = vUv;

		vec2 p2 = position + vec2(u_time/100.0);

		//with noise texture rb value
		position += (texture2D(u_tDisplacement, p2).rb - vec2(.5)) * AMOUNT;

		vec3 color = texture2D( u_tImage, position ).rgb;

		vec4 addedLights = vec4(0.0, 0.0, 0.0, 1.0);

		for(int l = 0; l < NUM_POINT_LIGHTS; l++) {
			vec3 lightDirection = normalize(vecPos  - pointLights[l].position);
			addedLights.rgb += clamp(dot(-lightDirection, vecNormal), 0.0, 1.0) * pointLights[l].color * u_lightIntensity;
		}

		gl_FragColor = vec4( color, u_alpha ) * addedLights;
	}

	</script>

	<!--ashima noise-->
	<script type="x-shader/x-fragment" id="fragmentAshimaNoise">

	varying vec2 vUv;
	varying vec3 vecPos;
	varying vec3 vecNormal;

	uniform vec2 u_resolution;
	uniform float u_time;
	uniform float u_alpha;

	uniform float u_lightIntensity;

	struct PointLight {
		vec3 color;
		vec3 position; // light position, in camera coordinates
		float distance; // used for attenuation purposes.
	};

	uniform PointLight pointLights[NUM_POINT_LIGHTS];

	//START ASHIMA NOISE

	//
	// Description : Array and textureless GLSL 2D/3D/4D simplex
	//               noise functions.
	//      Author : Ian McEwan, Ashima Arts.
	//  Maintainer : ijm
	//     Lastmod : 20110822 (ijm)
	//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
	//               Distributed under the MIT License. See LICENSE file.
	//               https://github.com/ashima/webgl-noise
	//

	vec3 mod289(vec3 x) {
		return x - floor(x * (1.0 / 289.0)) * 289.0;
	}

	vec4 mod289(vec4 x) {
		return x - floor(x * (1.0 / 289.0)) * 289.0;
	}

	vec4 permute(vec4 x) {
		return mod289(((x*34.0)+1.0)*x);
	}

	vec4 taylorInvSqrt(vec4 r)
	{
		return 1.79284291400159 - 0.85373472095314 * r;
	}

	float snoise(vec3 v)
	{
		const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
		const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

		// First corner
		vec3 i  = floor(v + dot(v, C.yyy) );
		vec3 x0 =   v - i + dot(i, C.xxx) ;

		// Other corners
		vec3 g = step(x0.yzx, x0.xyz);
		vec3 l = 1.0 - g;
		vec3 i1 = min( g.xyz, l.zxy );
		vec3 i2 = max( g.xyz, l.zxy );

		//   x0 = x0 - 0.0 + 0.0 * C.xxx;
		//   x1 = x0 - i1  + 1.0 * C.xxx;
		//   x2 = x0 - i2  + 2.0 * C.xxx;
		//   x3 = x0 - 1.0 + 3.0 * C.xxx;
		vec3 x1 = x0 - i1 + C.xxx;
		vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
		vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

		// Permutations
		i = mod289(i);
		vec4 p = permute( permute( permute(
			i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
			+ i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
			+ i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

			// Gradients: 7x7 points over a square, mapped onto an octahedron.
			// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
			float n_ = 0.142857142857; // 1.0/7.0
			vec3  ns = n_ * D.wyz - D.xzx;

			vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

			vec4 x_ = floor(j * ns.z);
			vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

			vec4 x = x_ *ns.x + ns.yyyy;
			vec4 y = y_ *ns.x + ns.yyyy;
			vec4 h = 1.0 - abs(x) - abs(y);

			vec4 b0 = vec4( x.xy, y.xy );
			vec4 b1 = vec4( x.zw, y.zw );

			//vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
			//vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
			vec4 s0 = floor(b0)*2.0 + 1.0;
			vec4 s1 = floor(b1)*2.0 + 1.0;
			vec4 sh = -step(h, vec4(0.0));

			vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
			vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

			vec3 p0 = vec3(a0.xy,h.x);
			vec3 p1 = vec3(a0.zw,h.y);
			vec3 p2 = vec3(a1.xy,h.z);
			vec3 p3 = vec3(a1.zw,h.w);

			//Normalise gradients
			vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
			p0 *= norm.x;
			p1 *= norm.y;
			p2 *= norm.z;
			p3 *= norm.w;

			// Mix final noise value
			vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
			m = m * m;
			return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
			dot(p2,x2), dot(p3,x3) ) );
		}

		//END ASHIMA NOISE

		void main(){

			vec2 position = vUv;
			float t = u_time * 0.1;

			vec3 col = vec3( abs(sin(u_time * 0.1)), position);

			float noise = snoise( vec3(position * 2. , t));
			float b = smoothstep(0.2, 0.3, noise);
			col += vec3( position, 1 ) * b;
			// col *= noise;
			//  vec3 col = vec3( noise);

			//add light
			vec4 addedLights = vec4(0.0, 0.0, 0.0, 1.0);

			for(int l = 0; l < NUM_POINT_LIGHTS; l++) {
				vec3 lightDirection = normalize(vecPos  - pointLights[l].position);
				addedLights.rgb += clamp(dot(-lightDirection, vecNormal), 0.0, 1.0) * pointLights[l].color * u_lightIntensity;
			}

			gl_FragColor = vec4(col,u_alpha) * addedLights;
		}

		</script>

		<!--end shaders-->

		<script src="./js/shader3dViewer.js">


		</script>

	</body>
	</html>
